pkgname=mate-video-player
pkgver=2011.08.31
pkgrel=1
pkgdesc="A MATE integrated movie player based on Gstreamer."
arch=(i686 x86_64)
license=('GPL')
depends=('desktop-file-utils' 'gstreamer0.10-base' 'gstreamer0.10-good-plugins' 'iso-codes' 'libunique' 'libxtst' 'libxxf86vm' 'python2' 'mate-pl-parser')
makedepends=('intltool' 'pkgconfig' 'mate-doc-utils')
options=('!emptydirs')
install=mate-video-player.install
url="http://matsusoft.com.ar/projects"
groups=('mate-extras')
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('8a65374ad544ad0d65e42eaf16ed092d00d892b438e30558d18c9e952b667aea')

build() {
	cd "${srcdir}/${pkgname}"

	./configure --prefix=/usr --sysconfdir=/etc \
		--libexecdir=/usr/lib/${pkgname} \
		--localstatedir=/var --disable-static || return 1

	make || return 1
}

package() {
	cd "${srcdir}/${pkgname}"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
