AC_INIT([MateConf Editor],
        [2.32.0],
        [http://bugzilla.mate.org/enter_bug.cgi?product=mateconf-editor],
        [mateconf-editor])
AC_CONFIG_SRCDIR([src/main.c])

AM_INIT_AUTOMAKE([1.9 foreign no-dist-gzip dist-bzip2])
AC_CONFIG_HEADERS([config.h])

IT_PROG_INTLTOOL([0.35.0])

AC_PROG_CC
AC_HEADER_STDC
AC_PROG_LIBTOOL
AC_PATH_PROG(MATECONFTOOL, mateconftool-2)
AM_MATECONF_SOURCE_2

AC_CONFIG_MACRO_DIR([m4])
MATE_DOC_INIT

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

AC_MSG_CHECKING([which gtk+ version to compile against])
AC_ARG_WITH([gtk],
  [AS_HELP_STRING([--with-gtk=2.0|3.0],[which gtk+ version to compile against (default: 2.0)])],
  [case "$with_gtk" in
     2.0|3.0) ;;
     *) AC_MSG_ERROR([invalid gtk version specified]) ;;
   esac],
  [with_gtk=2.0])
AC_MSG_RESULT([$with_gtk])

case "$with_gtk" in
  2.0) GTK_API_VERSION=2.0
       GTK_REQUIRED=2.18.0
       ;;
  3.0) GTK_API_VERSION=3.0
       GTK_REQUIRED=2.90.4
       ;;
esac

PKG_CHECK_MODULES(MATECONF_EDITOR,
		  mateconf-2.0 >= 2.9.2
		  gtk+-$GTK_API_VERSION >= $GTK_REQUIRED
		  dbus-glib-1)

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

GETTEXT_PACKAGE=mateconf-editor
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [The gettext package])

AM_GLIB_GNU_GETTEXT

MATE_MAINTAINER_MODE_DEFINES
MATE_COMMON_INIT
MATE_COMPILE_WARNINGS([maximum])

dnl Get the MateConf defaults source, and sed it to make the mandatory source.
MATECONF_DEFAULTS_SOURCE=`mateconftool-2 --get-default-source`
AC_ARG_WITH(mateconf-defaults-source,
  AC_HELP_STRING([--with-mateconf-defaults-source],
                 [The MateConf source of the default values]),
        MATECONF_DEFAULTS_SOURCE="$withval",)
AC_DEFINE_UNQUOTED(MATECONF_DEFAULTS_SOURCE, "$MATECONF_DEFAULTS_SOURCE", [MateConf defaults source])
AC_MSG_RESULT([Using config source $MATECONF_DEFAULTS_SOURCE for default values])

MATECONF_MANDATORY_SOURCE=`echo $MATECONF_DEFAULTS_SOURCE | sed -e s/mateconf.xml.defaults/mateconf.xml.mandatory/`
AC_ARG_WITH(mateconf-mandatory-source,
  AC_HELP_STRING([--with-mateconf-mandatory-source],
                 [The MateConf source of the mandatory values]),
        MATECONF_MANDATORY_SOURCE="$withval",)
AC_DEFINE_UNQUOTED(MATECONF_MANDATORY_SOURCE, "$MATECONF_MANDATORY_SOURCE", [MateConf mandatory source])
AC_MSG_RESULT([Using config source $MATECONF_MANDATORY_SOURCE for mandatory values])

AC_CONFIG_FILES([
Makefile
data/Makefile
data/mateconf-editor.desktop.in
data/icons/Makefile
docs/Makefile
src/Makefile
po/Makefile.in
])

AC_OUTPUT

echo "
Configuration Sources:
MateConf default values source:	${MATECONF_DEFAULTS_SOURCE}
MateConf mandatory values source:	${MATECONF_MANDATORY_SOURCE}"
