dnl AM_MATECONF_SOURCE_2
dnl Defines MATECONF_SCHEMA_CONFIG_SOURCE which is where you should install schemas
dnl  (i.e. pass to mateconftool-2
dnl Defines MATECONF_SCHEMA_FILE_DIR which is a filesystem directory where
dnl  you should install foo.schemas files
dnl

AC_DEFUN([AM_MATECONF_SOURCE_2],
[
  if test "x$MATECONF_SCHEMA_INSTALL_SOURCE" = "x"; then
    MATECONF_SCHEMA_CONFIG_SOURCE=`mateconftool-2 --get-default-source`
  else
    MATECONF_SCHEMA_CONFIG_SOURCE=$MATECONF_SCHEMA_INSTALL_SOURCE
  fi

  AC_ARG_WITH(mateconf-source, 
  [  --with-mateconf-source=sourceaddress      Config database for installing schema files.],MATECONF_SCHEMA_CONFIG_SOURCE="$withval",)

  AC_SUBST(MATECONF_SCHEMA_CONFIG_SOURCE)
  AC_MSG_RESULT([Using config source $MATECONF_SCHEMA_CONFIG_SOURCE for schema installation])

  if test "x$MATECONF_SCHEMA_FILE_DIR" = "x"; then
    MATECONF_SCHEMA_FILE_DIR='$(sysconfdir)/mateconf/schemas'
  fi

  AC_ARG_WITH(mateconf-schema-file-dir, 
  [  --with-mateconf-schema-file-dir=dir        Directory for installing schema files.],MATECONF_SCHEMA_FILE_DIR="$withval",)

  AC_SUBST(MATECONF_SCHEMA_FILE_DIR)
  AC_MSG_RESULT([Using $MATECONF_SCHEMA_FILE_DIR as install directory for schema files])

  AC_ARG_ENABLE(schemas-install,
     [  --disable-schemas-install	Disable the schemas installation],
     [case ${enableval} in
       yes|no) ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --enable-schemas-install) ;;
      esac])
  AM_CONDITIONAL([MATECONF_SCHEMAS_INSTALL], [test "$enable_schemas_install" != no])
])
